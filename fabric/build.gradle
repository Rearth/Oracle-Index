import java.util.zip.ZipEntry
import java.util.zip.ZipInputStream
import java.util.zip.ZipOutputStream

plugins {
    id 'com.github.johnrengelman.shadow'
}
loom {
    accessWidenerPath.set(project(":common").loom.accessWidenerPath)
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    // owo lib
    modImplementation "io.wispforest:owo-lib:0.12.11+1.21"

    // included libs
    // jsoup for html parsing
    implementation(include(api('org.jsoup:jsoup:1.17.2')))
    // commonmark for markdown parsing
    implementation(include(api('org.commonmark:commonmark:0.27.1')))
    implementation(include(api('org.commonmark:commonmark-ext-yaml-front-matter:0.27.1')))
    // exp4j for math expressions in search bar
    implementation(include(api('net.objecthunter:exp4j:0.4.8')))

    // langchain for in memory vector store and small embedding model
    implementation(include(api("dev.langchain4j:langchain4j:1.0.0")))
    implementation(include(api("dev.langchain4j:langchain4j-core:1.0.0")))
    implementation(include(api("dev.langchain4j:langchain4j-embeddings:1.0.0-beta5")))
    implementation(include(api("dev.langchain4j:langchain4j-embeddings-all-minilm-l6-v2-q:1.0.0-beta5")))
    implementation(include(api("ai.djl:api:0.31.1")))
    implementation(include(api("ai.djl.huggingface:tokenizers:0.31.1")))
    implementation(include(api("org.apache.opennlp:opennlp-tools:2.5.4")))
    implementation(include(api("com.microsoft.onnxruntime:onnxruntime:1.20.0")))



    modRuntimeOnly "teamreborn:energy:4.1.0"
    modImplementation "curse.maven:oritech-1030830:6520539" // oritech 0.15.5
    modRuntimeOnly "curse.maven:architectury-api-419699:5786326"
    modRuntimeOnly "curse.maven:athena-841890:5629396"
    modRuntimeOnly "curse.maven:geckolib-388172:5605709"
    modRuntimeOnly "curse.maven:emi-580555:6205505"
    // modRuntimeOnly "curse.maven:jei-238222:6516055"

    // simply swords for more oracle lib usage
    modImplementation "curse.maven:simply-swords-659887:6383325"
    modImplementation "curse.maven:fzzy-config-1005914:6405201"
    modImplementation("net.fabricmc:fabric-language-kotlin:1.13.3+kotlin.2.1.21")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

// because the stupid microsoft onnx package contains 50MB of unnecessary pdb files, we re-assemble the entire dist jar, and strip out those files
task cleanRemappedJar {

    doLast {
        File original = remapJar.archiveFile.get().asFile
        File temp = new File(original.parentFile, original.name + ".tmp")

        // Open streams for reading the old jar and writing the new one
        ZipInputStream zis = new ZipInputStream(new FileInputStream(original))
        ZipOutputStream zos = new ZipOutputStream(new FileOutputStream(temp))

        ZipEntry entry
        while ((entry = zis.nextEntry) != null) {
            String name = entry.name

            // Handle nested JARs in META-INF/jars/
            if (name.startsWith("META-INF/jars/")) {
                // Unzip the nested jar into memory, filter entries, and re-zip
                ByteArrayOutputStream baos = new ByteArrayOutputStream()
                ZipInputStream nestedIn  = new ZipInputStream(zis)
                ZipOutputStream nestedOut = new ZipOutputStream(baos)

                ZipEntry nestedEntry
                while ((nestedEntry = nestedIn.nextEntry) != null) {
                    String neName = nestedEntry.name
                    // skip pdb / macos debugger files
                    // skip osx and linux native support
                    if (!neName.endsWith(".pdb") && !neName.contains(".dSYM/") && !neName.contains("osx-") && !neName.contains("linux-")) {
                        nestedOut.putNextEntry(new ZipEntry(neName))
                        nestedIn.transferTo(nestedOut)
                        nestedOut.closeEntry()
                    }
                }
                nestedOut.close()
                // Write the filtered nested-jar back into the outer jar
                ByteArrayInputStream bais = new ByteArrayInputStream(baos.toByteArray())
                zos.putNextEntry(new ZipEntry(name))
                bais.transferTo(zos)
                zos.closeEntry()

                // Otherwise, skip any top-level .pdb or anything under .dSYM
            } else {
                zos.putNextEntry(new ZipEntry(name))
                zis.transferTo(zos)
                zos.closeEntry()
            }
            // else — we’ve hit a .pdb or .dSYM path, so we simply don’t copy it
        }

        // close everything
        zis.close()
        zos.close()

        // Replace the original jar
        original.delete()
        temp.renameTo(original)
    }
}

task xplatJar {

    doLast {
        File original = remapJar.archiveFile.get().asFile
        File temp = new File(original.parentFile, original.name.replace(".jar", "-xplat.jar"))

        // Open streams for reading the old jar and writing the new one
        ZipInputStream zis = new ZipInputStream(new FileInputStream(original))
        ZipOutputStream zos = new ZipOutputStream(new FileOutputStream(temp))

        ZipEntry entry
        while ((entry = zis.nextEntry) != null) {
            String name = entry.name

            // Handle nested JARs in META-INF/jars/
            if (name.startsWith("META-INF/jars/")) {
                // Unzip the nested jar into memory, filter entries, and re-zip
                ByteArrayOutputStream baos = new ByteArrayOutputStream()
                ZipInputStream nestedIn  = new ZipInputStream(zis)
                ZipOutputStream nestedOut = new ZipOutputStream(baos)

                ZipEntry nestedEntry
                while ((nestedEntry = nestedIn.nextEntry) != null) {
                    String neName = nestedEntry.name
                    // skip pdb / macos debugger files
                    // skip osx and linux native support
                    if (!neName.endsWith(".pdb") && !neName.contains(".dSYM/")) {
                        nestedOut.putNextEntry(new ZipEntry(neName))
                        nestedIn.transferTo(nestedOut)
                        nestedOut.closeEntry()
                    }
                }
                nestedOut.close()
                // Write the filtered nested-jar back into the outer jar
                ByteArrayInputStream bais = new ByteArrayInputStream(baos.toByteArray())
                zos.putNextEntry(new ZipEntry(name))
                bais.transferTo(zos)
                zos.closeEntry()

                // Otherwise, skip any top-level .pdb or anything under .dSYM
            } else {
                zos.putNextEntry(new ZipEntry(name))
                zis.transferTo(zos)
                zos.closeEntry()
            }
            // else — we’ve hit a .pdb or .dSYM path, so we simply don’t copy it
        }

        // close everything
        zis.close()
        zos.close()
    }
}

remapJar.finalizedBy(cleanRemappedJar)
remapJar.finalizedBy(xplatJar)

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    changelog = file("../CHANGELOG.md").getText()
    type = BETA
    modLoaders.add("fabric")

    modrinth {
        accessToken = providers.environmentVariable("MODRINTH_API_KEY")
        projectId = "J8MMsNrL"
        minecraftVersions.add("1.21.1")

        requires("owo-lib")
        requires("architectury-api")

        announcementTitle = "Fabric Version on Modrinth"
    }

    curseforge {
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        projectId = "1206582"
        minecraftVersions.add("1.21.1")

        requires("owo-lib")
        requires("architectury-api")

        javaVersions.add(JavaVersion.VERSION_21)
        clientRequired = true
        serverRequired = false

        projectSlug = "oritech"
        announcementTitle = "Fabric Version on CurseForge"
    }
}
